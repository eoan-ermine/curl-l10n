cmake_minimum_required(VERSION 3.12)
project(libcurl-l10n NONE)

if(NOT DEFINED LANG_CODE)
  message(FATAL_ERROR "You must specify -DLANG_CODE=xx (like -DLANG_CODE=ru)")
endif()

set(VERSION "8.16.0")
set(CPACK_PACKAGE_VERSION ${VERSION})

set(PO_SOURCE_DIR ${CMAKE_CURRENT_SOURCE_DIR}/source)
set(MO_OUTPUT_DIR ${CMAKE_BINARY_DIR}/mo)

set(PO_FILE ${PO_SOURCE_DIR}/${LANG_CODE}/libcurl.po)
set(DESC_FILE ${PO_SOURCE_DIR}/${LANG_CODE}/PACKAGE_DESCRIPTION)

if(NOT EXISTS ${PO_FILE})
  message(FATAL_ERROR "File not found: ${PO_FILE}")
endif()

if(NOT EXISTS ${DESC_FILE})
  message(FATAL_ERROR "Missing PACKAGE_DESCRIPTION file for language '${LANG_CODE}'")
endif()

file(READ ${DESC_FILE} PACKAGE_DESCRIPTION_STRING)
string(STRIP "${PACKAGE_DESCRIPTION_STRING}" PACKAGE_DESCRIPTION_STRING)

set(MO_TARGET_DIR ${MO_OUTPUT_DIR}/${LANG_CODE}/LC_MESSAGES)
set(MO_FILE ${MO_TARGET_DIR}/libcurl.mo)

file(MAKE_DIRECTORY ${MO_TARGET_DIR})

add_custom_command(
    OUTPUT ${MO_FILE}
    COMMAND msgfmt -o ${MO_FILE} ${PO_FILE}
    DEPENDS ${PO_FILE}
    COMMENT "Compiling ${PO_FILE} to ${MO_FILE}"
    VERBATIM
)

add_custom_target(compile ALL DEPENDS ${MO_FILE})

include(GNUInstallDirs)

set(CMAKE_INSTALL_DEFAULT_DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
install(FILES ${MO_FILE}
    DESTINATION ${CMAKE_INSTALL_LOCALEDIR}/${LANG_CODE}/LC_MESSAGES
    PERMISSIONS OWNER_READ OWNER_WRITE GROUP_READ WORLD_READ
)

set(CPACK_GENERATOR "DEB;RPM")
set(CPACK_PACKAGE_NAME "libcurl-l10n-${LANG_CODE}")
set(CPACK_PACKAGE_FILE_NAME "libcurl-l10n-${LANG_CODE}_${VERSION}_all")
set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE "all")
set(CPACK_DEBIAN_PACKAGE_MAINTAINER "Danil Sidoruk <danilsidoruknv@yandex.ru>")
set(CPACK_DEBIAN_PACKAGE_DESCRIPTION "${PACKAGE_DESCRIPTION_STRING}")
set(CPACK_RPM_PACKAGE_DESCRIPTION "${PACKAGE_DESCRIPTION_STRING}")
set(CPACK_RPM_PACKAGE_LICENSE "MIT")
set(CPACK_RPM_PACKAGE_ARCHITECTURE "all")
set(CPACK_RPM_PACKAGE_RELEASE "1")

include(CPack)
